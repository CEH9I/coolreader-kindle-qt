defaultTasks 'kindle'

apply plugin: 'java'

repositories {
  mavenCentral()
}

sourceCompatibility = 1.4
targetCompatibility = 1.4

ext.cr3 = !project.hasProperty("TouchRunner")

version = '3.0'
archivesBaseName = cr3 ? 'cr3runner' : 'TouchRunner'
ext.title = cr3 ? "Cool Reader" : "Touch Runner"

dependencies {
  compile files("lib/Kindlet-2.2.jar")
  compile files("lib/jailbreak.jar")
  testCompile group: 'junit', name: 'junit', version: '3.+'
}

ext.depClasses = { files("lib/jailbreak.jar").collect { it.isDirectory() ? it : zipTree(it) } }

jar {
  manifest {
    attributes("Manifest-Version": "1.0",
               "Main-Class":
                 cr3 ? "kz.pvnhome.cr3runner.CoolReaderRunnerKindlet" : "kz.pvnhome.cr3runner.TouchRunnerKindlet",
               "Implementation-Title": title,
               "Implementation-Version": version,
               "Implementation-Vendor": "CrazyCoder",
               "Amazon-Cover-Image": cr3 ? "cover.png" : "cover-touch.png",
               "Extension-List": "SDK",
               "SDK-Extension-Name": "com.amazon.kindle.kindlet",
               "SDK-Specification-Version": "2.1",
               "Toolbar-Mode": "persistent",
               "Font-Size-Mode": "point")
  }
  from(depClasses) {
    exclude 'META-INF/MANIFEST.MF'
    exclude '**/*.java'
  }
  from files(cr3 ? "cover.png" : "cover-touch.png")
}

task copyJarToTmp(dependsOn: jar, type: Copy) {
  from(jar.archivePath)
  into(jar.destinationDir)
  rename '(.+)\\.jar', '$1.tmp'
}

task signingJarTask(dependsOn: copyJarToTmp) << {
  println "Signing: $jar.archivePath"
  signingJar('dk')
  signingJar('di')
  signingJar('dn')
}

task kindle(dependsOn: signingJarTask, type: Copy) {
  from(jar.archivePath)
  into(jar.destinationDir)
  rename '(.+)\\.jar', '$1.azw2'
}

void signingJar(String keyPref) {
  println "pref: $keyPref"
  ant.exec(dir: env_keystoreDir, executable: "jarsigner") {
    arg(line: "-keystore $key_keyStore -storepass $key_storePass $jar.archivePath $keyPref$key_keyAlias")
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.9'
}
